import("//tools/grit/grit_rule.gni")

transpile_includes = [
  # Brave Adblock Page
  "../brave_adblock_ui/brave_adblock.html",
  # Brave New Tab
  "../brave_new_tab_ui/brave_new_tab.html",
  # Brave Rewards
  "../brave_rewards_ui/brave_rewards.html",
  # Brave Welcome Page
  "../brave_welcome_ui/brave_welcome.html",
  "../brave_welcome_ui/brave_welcome.html",
  # Brave sync
  "../brave_sync_ui/brave_sync.html",
  "../brave_sync_ui/brave_sync.js",
  # Brave sync lib
  "../brave_sync_lib/brave_sync_lib.html",
  "../brave_sync_lib/brave_sync_lib.js",
  # QR lib for Brave sync
  "//brave/node_modules/qrcode-generator/qrcode.js",
  # Brave sync js bundle
  "$root_gen_dir/brave-sync/bundles/bundle.js",
  # Brave Crypto for Brave sync
  "$root_gen_dir/brave-crypto/browser/crypto.js",
]

action("transpile_web_uis") {
  script = "//brave/script/transpile-web-ui.py"
  inputs = transpile_includes
  outputs = [
    "$target_out_dir/brave_new_tab.bundle.js",
    "$target_out_dir/brave_rewards.bundle.js",
    "$target_out_dir/brave_welcome.bundle.js",
    "$target_out_dir/brave_adblock.bundle.js",
    "$target_out_dir/brave_sync.bundle.js",
  ]
  gen_dir = rebase_path(root_gen_dir, "//")
  args = [
    "--target_gen_dir=$gen_dir"
  ]
  if (is_official_build) {
    args += [
      "--production",
    ]
  }
  public_deps = [
    "//brave/components/brave_sync_lib:brave_sync_js_lib_bundle",
    "//brave/components/brave_sync_lib:brave_crypto_js_lib_bundle",
  ]
}

grit("brave_components_resources_grit") {
  source = "brave_components_resources.grd"

  output_name = "brave_components_resources_new"
  inputs = get_target_outputs(":transpile_web_uis")
  inputs += transpile_includes

  outputs = [
    "grit/brave_components_resources.h",
    "brave_components_resources.pak",
  ]
  deps = [ ":transpile_web_uis" ]
  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]
  output_dir = "$root_gen_dir/components"
  resource_ids = "//brave/browser/resources/resource_ids"
}
