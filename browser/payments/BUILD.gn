declare_args() {
  # anonize doesn't currently build on windows
  brave_payments_enabled = is_mac || is_linux
}

config("external_config") {
  visibility = [ ":payments" ]

  defines = []
  if (brave_payments_enabled) {
    defines += [ "BRAVE_PAYMENTS_ENABLED" ]
  }
}

source_set("payments") {
  public_configs = [
    ":external_config"
  ]

  sources = [
    "content_site.cc",
    "content_site.h",
    "payments_service.cc",
    "payments_service.h",
    "payments_service_factory.cc",
    "payments_service_factory.h",
    "payments_service_observer.h",
    "publisher_info_backend.cc",
    "publisher_info_backend.h",
  ]

  deps = [
    "//base",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    # for profile.h
    "//components/domain_reliability",
    "//content/public/browser",
    "//services/network/public/mojom",
    "//third_party/leveldatabase",
  ]

  if (brave_payments_enabled) {
    sources += [
      "payments_service_impl.cc",
      "payments_service_impl.h",
    ]

    deps += [
      "//brave/vendor/bat-native-ledger",
      "//net",
      "//url",
    ]
  }
}
